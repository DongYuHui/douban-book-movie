apply plugin: 'com.android.application'
// ButterKnife
apply plugin: 'android-apt'
// RetroLambda
apply plugin: 'me.tatarka.retrolambda'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId "com.kyletung.simplebookmovie"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            buildConfigField("String", "BASE_URL", "\"https://api.douban.com/v2/\"")
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField("String", "BASE_URL", "\"https://api.douban.com/v2/\"")
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        global {
            applicationId "com.kyletung.simplebookmovie"
        }
        china {
            applicationId "com.kyletung.doubanbookmovie"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile rootProject.ext.dependencies.cardview
    compile rootProject.ext.dependencies.appcompatV7
    compile rootProject.ext.dependencies.recyclerview
    // Library
    compile project(':commonlib')
    // ButterKnife
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    // Blur
    compile 'net.qiujuer.genius:blur:2.0.0'
    // Bottom Bar
    compile 'com.ashokvarma.android:bottom-navigation-bar:1.3.0'
}
